#! /bin/csh -f

# uttest: 实用工具（UTILITY）层测试脚本

#
# DATADIR:  存放测试用数据文件的目录  
#

set DATADIR = ./uttestdata


#
# TESTSDIR:  存放测试用查询（UBASE命令）的目录  
#

set TESTSDIR = ./uttestqueries


#
# Don't change this, unless you want to go and change all of the
# queries in the test files.
#

set LOCALNAME = data


#
# 3个前端使用工具的名称
#

set DBCREATE  = ./dbcreate
set DBDESTROY = ./dbdestroy
set UBASE   = ./ubase


#
# Before doing anything else, we have to create a symbolic link to the
# data directory if one doesn't already exist.  This is because the
# test queries expect to find the data files in a directory called
# `data'.
#

if ( -d data ) goto DATAOK

echo You need to have a directory called \`$LOCALNAME\' in order \
	to run this script.
echo -n "Shall I create one?  (y or n) "

if ( $< == n ) then
	echo $0 aborted
	exit 1
endif

echo ''

if ( ! -d $DATADIR ) then
	echo I can not find a directory called $DATADIR. \
		Please check the value of the DATADIR variable \
		in the $0 script and try again. | fmt
	exit 1
endif

if ( ! -r $DATADIR/soaps.data ) then
	echo I can not find the necessary data files in $DATADIR. \
		Please check the value of the DATADIR variable in \
		the $0 script and try again. | fmt
	exit 1
endif

ln -s $DATADIR $LOCALNAME >& /dev/null

if ( $status == 0 ) goto DATAOK

if ( ! -w . ) then
	echo You do not have permission to create files in this \
		'directory.  Please fix the permissions and rerun \
		this script. | fmt
	exit 1
endif

echo I can not make the directory.  If you have a file called \
	\`$LOCALNAME\' in this directory, remove it and run this \
	script again.  If not, please send mail to cs564. | fmt
exit 1


DATAOK:


#
# Now that the data directory is set up, make sure that the TESTSDIR
# variable is set to something reasonable
#

if ( ! -d $TESTSDIR ) then
	echo The TESTSDIR variable is currently set to \
		$TESTSDIR, which is not a valid directory. \
		Please read the instructions at the top of the \
		$0 script, set 'TESTDIR' correctly, and rerun the \
		script. | fmt
	exit 1
endif

if ( `ls $TESTSDIR/ut.[0-9]* | wc -l` == 0 ) then
	echo I can not find the UT test files in $TESTSDIR. \
		Please read the instructions at the beginning \
		of the $0 script, set TESTDIR correctly, and rerun \
		the script | fmt
	exit 1
endif


#
# 本测试将要使用的数据库名称
#
set TESTDB = testdb

#
# 运行指定的测试
#

#
# 如果没有指定测试编号，则运行全部测试
#
if ( $#argv == 0 ) then
    foreach queryfile ( `ls $TESTSDIR/ut.*` )
	echo "运行测试 #" $queryfile:e '*****************************************'
	$DBCREATE  $TESTDB
	$UBASE   $TESTDB < $queryfile
	echo "y" | $DBDESTROY $TESTDB
    end

#
# 如果指定测试了编号，在运行指定的测试
#
else
    foreach testnum ( $* )
	if ( -r $TESTSDIR/ut.$testnum ) then
            echo "运行测试 #" $testnum '*****************************************'
            $DBCREATE  $TESTDB
            $UBASE   $TESTDB < $TESTSDIR/ut.$testnum
            echo "y" | $DBDESTROY $TESTDB
	else
            echo "找不到编号为"$testnum"的测试文件"
	endif
    end
endif
